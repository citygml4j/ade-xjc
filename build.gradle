import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
}

group 'org.citygml4j'
version '2.10.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

ext {
    citygml4jVersion = '2.11'
    date = new Date()
}

dependencies {
    implementation 'args4j:args4j:2.33'
    implementation 'org.glassfish.jaxb:jaxb-xjc:2.3.2'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Specification-Version': citygml4jVersion,
                'Main-Class': application.mainClass,
                'Class-Path': configurations.runtimeClasspath.collect{ it.name }.join(' ')
        )
        into('META-INF') {
            from 'LICENSE'
        }
    }
}

application {
    mainClass = 'org.citygml4j.ade_xjc.ADESchemaCompiler'
    executableDir = ''

    startScripts {
        defaultJvmOpts = ['-Djavax.xml.accessExternalSchema=file']
        doLast {
            def windowsScript = getWindowsScript()
            def unixScript = getUnixScript()
            windowsScript.text = windowsScript.text.replaceFirst('set APP_HOME=.*', '$0\r\nset WORKING_DIR=%CD%')
            unixScript.text = unixScript.text.replaceFirst('APP_HOME=.*', '$0\nexport APP_HOME\nexport WORKING_DIR=\\$SAVED')
        }
    }
}

distributions.main.contents {
    from('resources/doc/README') {
        filter(ReplaceTokens, tokens: [
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: sourceCompatibility.toString(),
                appName: project.name
        ])
    }
    into('jaxb-plugins') {
        from 'jaxb-plugins'
    }
    into('schemas') {
        from 'schemas'
    }
    into('sample') {
        from 'sample'
    }
    into('license') {
        from 'LICENSE'
    }
}